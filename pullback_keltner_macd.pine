//@version=4
strategy("Pullback Keltner Channel Strategy", overlay=true)

// BEGIN TIME WINDOW
var from_year  = input(defval = 2019, title = "From Year", minval = 2010)
var from_month = input(defval = 1, title = "From Month", minval = 1, maxval = 12)
var from_day   = input(defval = 1, title = "From Day", minval = 1, maxval = 31)
var to_year    = input(defval = 9999, title = "To Year", minval = 2011)
var to_month   = input(defval = 1, title = "To Month", minval = 1, maxval = 12)
var to_day     = input(defval = 1, title = "To Day", minval = 1, maxval = 31)
var window_start  = timestamp(from_year, from_month, from_day, 00, 00) 
var window_finish = timestamp(to_year, to_month, to_day, 23, 59)   
window() => time >= window_start and time <= window_finish ? true : false
// END TIME WINDOW

var ttf_res = input(title="Trading Timeframe", type=input.resolution, defval="60")

// BEGIN KELTNER CHANNEL
var kc_true_range = input(true)
var kc_len = input(21)
var kc_mul = input(1.5)

kc_center = ema(close, kc_len)
kc_range = kc_true_range ? tr : high - low
kc_range_ema = ema(kc_range, kc_len)
kc_upper = kc_center + kc_range_ema * kc_mul
kc_lower = kc_center - kc_range_ema * kc_mul

kc_upper_htf = security(syminfo.tickerid, ttf_res, kc_upper)
kc_lower_htf = security(syminfo.tickerid, ttf_res, kc_lower)
kc_center_htf = security(syminfo.tickerid, ttf_res, kc_center)
// END KELTNER CHANNEL

plot(kc_upper_htf, color=color.white)
plot(kc_lower_htf, color=color.white)
plot(kc_center_htf, color=color.white)

var setup_long = false

check_setup_long() => 
    if strategy.position_size == 0 
        if low <= kc_lower_htf
            false
        else
            setup_long or high >= kc_upper_htf
    else
        false
        
setup_long := check_setup_long()

enter_long() =>
    if setup_long and high <= kc_upper_htf
        low <= kc_center_htf
    else
        false

atr = security(syminfo.tickerid, ttf_res, sma(tr, 14))


strategy.entry(id="LONG", long=true, limit=close, when=enter_long() and window())
strategy.exit(id="LONG", stop=close-atr, profit=kc_upper_htf)


bgcolor(setup_long ? color.yellow : na)
bgcolor(strategy.position_size > 0 ? color.green : na)

//plotshape(1, title='SIGNAL', color=state == 0 ? color.green : color.red, style=shape.circle, location=location.absolute)
//plotshape(1, title='SETUP', color=setup_long ? color.green : color.red, style=shape.circle, location=location.absolute)
//plotshape(2, title='ENTRY', color=enter_long ? color.green : color.red, style=shape.circle, location=location.absolute)
//plotshape(4, title='SL/TP',  color=state == 3 ? color.green : color.red, style=shape.circle, location=location.absolute)


